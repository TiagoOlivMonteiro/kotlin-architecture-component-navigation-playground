// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: "$rootDir/app/versions.gradle"
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android_plugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.androidx.navigation"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    afterEvaluate {
        if (!project.hasProperty("android")) return
        android {
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            defaultConfig {
                multiDexEnabled false
            }

            buildTypes {
                debug {
                    testCoverageEnabled true
                }
            }

            testOptions {
                animationsDisabled true
                unitTests.includeAndroidResources true
                unitTests.returnDefaultValues true
            }

            dexOptions {
                preDexLibraries true
            }

        }
    }

    configurations.all {
        exclude module: 'listenablefuture'

        resolutionStrategy {
            eachDependency {
                if (requested.group == 'androidx.test' && requested.name == 'monitor') {
                    useVersion versions.androidx.astl
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
